import gg.jte.ContentType
import gg.jte.gradle.GenerateJteTask

plugins {
    id 'java'
    id 'gg.jte.gradle' version '3.2.3'
}

repositories {
    mavenCentral()
    mavenLocal()
}

group = 'gg.jte.testgroup'

test {
    useJUnitPlatform()
}

dependencies {
    implementation('gg.jte:jte-runtime:3.2.3')
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
    testImplementation('org.assertj:assertj-core:3.23.1')
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    implementation 'info.picocli:picocli:4.6.1'
    annotationProcessor 'info.picocli:picocli-codegen:4.6.1'
    jteGenerate('gg.jte:jte-native-resources:3.2.3')
}

def additionalGenerateTask = tasks.register("additionalGenerateTask", GenerateJteTask) {
    contentType = ContentType.Html
    sourceDirectory = file("src/main/jte").toPath()
    targetDirectory = file("build/additionalGenerateTask").toPath()
    packageName = "gg.jte.additionalgeneratetask"
}

tasks.named("compileJava") {
    dependsOn(additionalGenerateTask)
}

sourceSets.named("main") {
    java.srcDir(additionalGenerateTask.map {it.targetDirectory})
}