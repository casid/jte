import gg.jte.ContentType
import java.nio.file.Paths
import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'gg.jte.gradle' version '1.11.3-SNAPSHOT'
    id "com.palantir.graal" version "0.9.0" /* for native image
        There are a few native image plugins around. I used this one because it manages downloading and caching GraalVM
        if necessary. */
}

repositories {
    mavenCentral()
    mavenLocal()
}

group = 'gg.jte.testgroup'

sourceSets {
    nativeMain {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
    nativeTest {
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation('gg.jte:jte-runtime:1.11.3-SNAPSHOT')
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
    testImplementation('org.assertj:assertj-core:3.20.0')
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    nativeMainImplementation 'info.picocli:picocli:4.6.1'
    nativeMainAnnotationProcessor 'info.picocli:picocli-codegen:4.6.1'

}

configurations {
    nativeMainImplementation.extendsFrom implementation
    nativeTestImplementation.extendsFrom testImplementation
    nativeTestRuntimeOnly.extendsFrom testRuntimeOnly
}

jar {
    from sourceSets.nativeMain.output.classesDirs
    from sourceSets.nativeMain.output.resourcesDir
}

tasks.generateJte {
    sourceDirectory = Paths.get(project.projectDir.absolutePath, "src", "main", "jte")
    contentType = gg.jte.ContentType.Html
    generateNativeImageResources = true
}

sourceSets.main.java.srcDir(tasks.generateJte.targetDirectory)
sourceSets.main.resources.srcDir(tasks.generateJte.targetResourceDirectory)

tasks.compileJava {
    dependsOn(tasks.generateJte)
}

compileNativeMainJava {
    // see https://picocli.info/#_processor_option_project
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

// GraalVM currently only has versions 8 or 11
String graalJavaVersion() {
    def javaMajorVersion = targetCompatibility.majorVersion as int
    if (javaMajorVersion <= 8 && Os.isFamily(Os.FAMILY_WINDOWS)) {
        // GraalVM for Java 8 on Windows requires a difficult to install SDK version
        return 'skip'
    }
    switch (javaMajorVersion) {
        case { it <= 8 }:
            return '8'
        case { it <= 11 }:
            return '11'
        default:
            return 'skip'
    }
}

graal {
    mainClass 'gg.jte.NativeTestApp'
    outputName 'nativeTestApp'
    javaVersion(graalJavaVersion() == 'skip' ? '11' : graalJavaVersion())
}

tasks.nativeImage {
    onlyIf { graalJavaVersion() != 'skip' }
    dependsOn compileNativeMainJava
    classpath.set configurations.nativeMainRuntimeClasspath
}

task nativeTest(type: Test) {
    onlyIf { graalJavaVersion() != 'skip' }
    dependsOn nativeImage
    description = "Run test of native build"
    group = "verification"
    systemProperty("nativeApp", tasks.nativeImage.outputFile.get().toString())
    testClassesDirs = sourceSets.nativeTest.output.classesDirs
    classpath = sourceSets.nativeTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
}

task skippedNativeTest {
    onlyIf { graalJavaVersion() == 'skip' }
    doLast {
        println "Skipped native-image test, incompatible Java version"
    }
}

check.dependsOn(nativeTest, skippedNativeTest)
